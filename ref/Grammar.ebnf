(* Program *)
Program          ::= { Statement } EOF ;

(* Statements – ordered to keep the LL(1) parser unambiguous *)
Statement        ::= FunctionDef
                   | ClassDef
                   | ImportStmt
                   | GlobalStmt
                   | VarDecl
                   | AugAssignment
                   | Assignment
                   | IfStmt
                   | WhileStmt
                   | ForStmt
                   | TryStmt
                   | RaiseStmt
                   | ReturnStmt
                   | AssertStmt
                   | BreakStmt
                   | ContinueStmt
                   | PassStmt
                   | ExprStmt ;

FunctionDef      ::= "def" Identifier "(" [ ParamList ] ")" "->" Type
                     ":" NEWLINE INDENT { Statement } DEDENT ;
ParamList        ::= Param { "," Param } ;
Param            ::= Identifier ":" Type ;

ClassDef         ::= "class" Identifier [ "(" Identifier ")" ]
                     ":" NEWLINE INDENT { Statement } DEDENT ;

ImportStmt       ::= "import" Identifier { "." Identifier } [ "as" Identifier ] NEWLINE ;
GlobalStmt       ::= "global" Identifier { "," Identifier } NEWLINE ;

VarDecl          ::= Identifier ":" Type "=" Expression NEWLINE ;
Assignment       ::= Identifier "=" Expression NEWLINE ;
AugAssignment    ::= Identifier
                     ( "+=" | "-=" | "*=" | "/=" | "//=" | "%=" )
                     Expression NEWLINE ;

ReturnStmt       ::= "return" [ Expression ] NEWLINE ;
AssertStmt       ::= "assert" Expression NEWLINE ;
RaiseStmt        ::= "raise" Identifier NEWLINE ;
BreakStmt        ::= "break" NEWLINE ;
ContinueStmt     ::= "continue" NEWLINE ;
PassStmt         ::= "pass" NEWLINE ;
ExprStmt         ::= Expression NEWLINE ;

IfStmt           ::= "if" Expression ":" NEWLINE INDENT { Statement } DEDENT
                     [ ElifElseBlock ] ;
ElifElseBlock    ::= { ElifBlock } [ ElseBlock ] | ElseBlock ;
ElifBlock        ::= "elif" Expression ":" NEWLINE INDENT { Statement } DEDENT ;
ElseBlock        ::= "else" ":" NEWLINE INDENT { Statement } DEDENT ;

WhileStmt        ::= "while" Expression ":" NEWLINE INDENT { Statement } DEDENT ;
ForStmt          ::= "for" Identifier "in" Expression ":"
                     NEWLINE INDENT { Statement } DEDENT ;

TryStmt          ::= "try" ":" NEWLINE INDENT { Statement } DEDENT
                     { ExceptBlock } ;
ExceptBlock      ::= "except" Identifier ":" NEWLINE INDENT { Statement } DEDENT ;

(* Expressions – precedence, lowest → highest *)
Expression       ::= OrExpr ;

OrExpr           ::= AndExpr { "or" AndExpr } ;
AndExpr          ::= EqualityExpr { "and" EqualityExpr } ;
EqualityExpr     ::= RelationalExpr
                     { ( "==" | "!=" | "is" | "not" ) RelationalExpr } ;
RelationalExpr   ::= AddExpr { ( "<" | "<=" | ">" | ">=" ) AddExpr } ;
AddExpr          ::= MulExpr { ( "+" | "-" ) MulExpr } ;
MulExpr          ::= UnaryExpr { ( "*" | "/" | "//" | "%" ) UnaryExpr } ;
UnaryExpr        ::= [ "not" | "-" ] PrimaryExpr ;
PrimaryExpr      ::= Atom { "[" Expression "]" | "." Identifier | "(" [ ArgList ] ")" } ;
Atom             ::= ListExpr
                   | DictExpr
                   | CallExpr
                   | Number
                   | FString
                   | String
                   | Bool
                   | Identifier
                   | "(" Expression ")" ;

ListExpr         ::= "[" [ Expression { "," Expression } ] "]" ;

DictExpr         ::= "{" [ DictItems ] "}"
DictItems        ::= DictItem { "," DictItem }
DictItem         ::= Expression ":" Expression

(* Argument lists for calls *)
CallExpr         ::= Identifier "(" [ ArgList ] ")" ;
ArgList          ::= Expression { "," Expression } ;

(* Parameters’ types *)
Type             ::= BaseType | GenericType ;
BaseType         ::= "int" | "float" | "bool" | "str" | Identifier ;
GenericType      ::= Identifier "[" TypeList "]" ;
TypeList         ::= Type { "," Type } ;

(* Terminals *)

Identifier       ::= ? [A-Za-z_][A-Za-z0-9_]* ? ;

Number           ::= ? 
  [0-9] [0-9_]* 
  ( "." [0-9] [0-9_]* )? 
  ( [eE] [+\-]? [0-9] [0-9_]* )? 
? ;

String           ::= ?
  ( '"' ( '\\' . | [^"\\] )* '"' 
  | '\'' ( '\\' . | [^'\\] )* '\'' )
? ;

FString          ::= FSTRING_START { FStringPart } FSTRING_END ;
FStringPart      ::= FSTRING_MIDDLE | FStringExpr ;
FStringExpr      ::= LBRACE Expression RBRACE ;

Bool             ::= "True" | "False" ;

NEWLINE          ::= ?   (* end of line, always emitted, even on blank/comment-only lines *)   ? ;
INDENT, DEDENT   ::= (* produced by the lexer, column=1 *) ;
EOF              ::= (* end-of-file *) ;
